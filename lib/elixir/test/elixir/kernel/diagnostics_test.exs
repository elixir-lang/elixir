Code.require_file("../test_helper.exs", __DIR__)

defmodule Kernel.DiagnosticsTest do
  use ExUnit.Case, async: false

  import ExUnit.CaptureIO

  setup do
    Application.put_env(:elixir, :ansi_enabled, false)
    on_exit(fn -> Application.put_env(:elixir, :ansi_enabled, true) end)
  end

  describe "compile-time exceptions" do
    test "SyntaxError (snippet)" do
      expected = """
      ** (SyntaxError) invalid syntax found on nofile:1:17:
          â”Œâ”€ error: nofile:1:17
          â”‚
        1 â”‚ [1, 2, 3, 4, 5, *]
          â”‚                 ^
          â”‚
          syntax error before: '*'
      """

      output =
        capture_raise(
          """
          [1, 2, 3, 4, 5, *]
          """,
          SyntaxError
        )

      assert output == expected
    end

    test "TokenMissingError (snippet)" do
      expected = """
      ** (TokenMissingError) token missing on nofile:1:4:
          â”Œâ”€ error: nofile:1:4
          â”‚
        1 â”‚ 1 +
          â”‚    ^
          â”‚
          syntax error: expression is incomplete
      """

      output =
        capture_raise(
          """
          1 +
          """,
          TokenMissingError
        )

      assert output == expected
    end

    test "TokenMissingError (no snippet)" do
      expected = """
      ** (TokenMissingError) token missing on nofile:2:1:
          â”Œâ”€ error: nofile:2:1
          missing terminator: end (for "fn" starting at line 1)
      """

      output =
        capture_raise(
          """
          fn a
          """,
          TokenMissingError
        )

      assert output == expected
    end

    test "keeps trailing whitespace if under threshold" do
      expected = """
      ** (SyntaxError) invalid syntax found on nofile:1:23:
          â”Œâ”€ error: nofile:1:23
          â”‚
        1 â”‚                   a + ðŸ˜Ž
          â”‚                       ^
          â”‚
          unexpected token: "ðŸ˜Ž" (column 23, code point U+****)
      """

      output =
        capture_raise(
          """
                            a + ðŸ˜Ž
          """,
          SyntaxError
        )

      assert output == expected
    end

    test "limits trailing whitespace if too many" do
      expected = """
      ** (SyntaxError) invalid syntax found on nofile:1:43:
          â”Œâ”€ error: nofile:1:43
          â”‚
        1 â”‚ ...                   a + ðŸ˜Ž
          â”‚                           ^
          â”‚
          unexpected token: "ðŸ˜Ž" (column 43, code point U+****)
      """

      output =
        capture_raise(
          """
                                                a + ðŸ˜Ž
          """,
          SyntaxError
        )

      assert output == expected
    end

    test "shows stacktrace if present" do
      fake_stacktrace = [
        {:fake, :fun, 3, [file: "nofile", line: 10]},
        {:real, :fun, 2, [file: "nofile", line: 10]}
      ]

      expected = """
      ** (TokenMissingError) token missing on nofile:1:4:
          â”Œâ”€ error: nofile:1:4
          â”‚
        1 â”‚ 1 -
          â”‚    ^
          â”‚
          syntax error: expression is incomplete

          nofile:10: :fake.fun/3
          nofile:10: :real.fun/2
      """

      output =
        capture_raise(
          """
          1 -
          """,
          TokenMissingError,
          fake_stacktrace
        )

      assert output == expected
    end

    test "2-digit line errors stay aligned 1-digit line errors" do
      fake_stacktrace = [
        {:fake, :fun, 3, [file: "nofile", line: 10]}
      ]

      expected = """
      ** (TokenMissingError) token missing on nofile:12:4:
          â”Œâ”€ error: nofile:12:4
          â”‚
       12 â”‚ 1 -
          â”‚    ^
          â”‚
          syntax error: expression is incomplete

          nofile:10: :fake.fun/3
      """

      output =
        capture_raise(
          """
          #{String.duplicate("\n", 10)}
          1 -
          """,
          TokenMissingError,
          fake_stacktrace
        )

      assert output == expected
    end

    test "handles unicode" do
      source = """
      defmodule Sample do
        def a do
          10 + ðŸ˜Ž
        end
      end
      """

      output = capture_raise(source, SyntaxError)

      assert output =~ "ðŸ˜Ž"
    after
      purge(Sample)
    end
  end

  describe "compiler warnings" do
    @tag :tmp_dir
    test "simple warning (line + column + file)", %{tmp_dir: tmp_dir} do
      path = make_relative_tmp(tmp_dir, "long-warning.ex")

      source = """
      defmodule Sample do
        @file "#{path}"
        defp a, do: Unknown.b()
      end
      """

      File.write!(path, source)

      expected = """
          â”Œâ”€ warning: #{path}:3:22: Sample.a/0
          â”‚
        3 â”‚   defp a, do: Unknown.b()
          â”‚                      ~
          â”‚
          Unknown.b/0 is undefined (module Unknown is not available or is yet to be defined)

      """

      assert capture_eval(source) =~ expected
    after
      purge(Sample)
    end

    @tag :tmp_dir
    test "simple warning (line + file)", %{tmp_dir: tmp_dir} do
      path = make_relative_tmp(tmp_dir, "long-warning.ex")

      source = """
      defmodule Sample do
        @file "#{path}"
        defp a, do: Unknown.b()
      end
      """

      File.write!(path, source)

      expected = """
          â”Œâ”€ warning: #{path}:3: Sample.a/0
          â”‚
        3 â”‚   defp a, do: Unknown.b()
          â”‚   ~~~~~~~~~~~~~~~~~~~~~~~
          â”‚
          Unknown.b/0 is undefined (module Unknown is not available or is yet to be defined)

      """

      assert capture_eval(source, false) =~ expected
    after
      purge(Sample)
    end

    test "simple warning (no file)" do
      source = """
      defmodule Sample do
        defp a, do: Unknown.b()
      end
      """

      expected = """
       â”Œâ”€ warning: nofile:2:22: Sample.a/0
       Unknown.b/0 is undefined (module Unknown is not available or is yet to be defined)

      """

      assert capture_eval(source) =~ expected
    after
      purge(Sample)
    end

    @tag :tmp_dir
    test "long message (file)", %{tmp_dir: tmp_dir} do
      path = make_relative_tmp(tmp_dir, "long-warning.ex")

      source = """
      defmodule Sample do
        @file "#{path}"

        def atom_case do
          v = "bc"

          case v do
            _ when is_atom(v) -> :ok
            _ -> :fail
          end
        end
      end
      """

      File.write!(path, source)

      expected = """
          â”Œâ”€ warning: #{path}:8:14: Sample.atom_case/0
          â”‚
        8 â”‚       _ when is_atom(v) -> :ok
          â”‚              ~
          â”‚
          incompatible types:
          
              binary() !~ atom()
          
          in expression:
          
              # #{path}:8
              is_atom(v)
          
          where "v" was given the type binary() in:
          
              # #{path}:5
              v = "bc"
          
          where "v" was given the type atom() in:
          
              # #{path}:8
              is_atom(v)
          
          Conflict found at

      """

      assert capture_eval(source) =~ expected
    after
      purge(Sample)
    end

    test "long message (nofile)" do
      source = """
      defmodule Sample do
        def atom_case do
          v = "bc"

          case v do
            _ when is_atom(v) -> :ok
            _ -> :fail
          end
        end
      end
      """

      expected = """
       â”Œâ”€ warning: nofile:6:14: Sample.atom_case/0
       incompatible types:
       
           binary() !~ atom()
       
       in expression:
       
           # nofile:6
           is_atom(v)
       
       where "v" was given the type binary() in:
       
           # nofile:3
           v = "bc"
       
       where "v" was given the type atom() in:
       
           # nofile:6
           is_atom(v)
       
       Conflict found at

      """

      assert capture_eval(source) =~ expected
    after
      purge(Sample)
    end

    @tag :tmp_dir
    test "trims lines if too many whitespaces", %{tmp_dir: tmp_dir} do
      path = make_relative_tmp(tmp_dir, "trim_warning_line.ex")

      source = """
      defmodule Sample do
        @file "#{path}"

        def a do
                                                  Unknown.bar(:test)
        end
      end
      """

      File.write!(path, source)

      expected = """
          â”Œâ”€ warning: #{path}:5:52: Sample.a/0
          â”‚
        5 â”‚ ...                   Unknown.bar(:test)
          â”‚                              ~
          â”‚
          Unknown.bar/1 is undefined (module Unknown is not available or is yet to be defined)

      """

      assert capture_eval(source) == expected
    after
      purge(Sample)
    end

    @tag :tmp_dir
    test "handles unicode", %{tmp_dir: tmp_dir} do
      path = make_relative_tmp(tmp_dir, "warning_group_unicode.ex")

      source = """
      defmodule Sample do
        @file "#{path}"

        def a do
          Unknown.bar("ðŸ˜Ž")
          Unknown.bar("ðŸ˜Ž")
        end
      end
      """

      File.write!(path, source)

      assert capture_eval(source) =~ "ðŸ˜Ž"
    after
      purge(Sample)
    end
  end

  describe "warning groups" do
    test "no file" do
      source = """
      defmodule Sample do
        def a do
          Unknown.bar()
          Unknown.bar()
          Unknown.bar()
          Unknown.bar()
        end
      end
      """

      expected = """
       â”Œâ”€ warning: nofile:3:12: Sample.a/0
       Unknown.bar/0 is undefined (module Unknown is not available or is yet to be defined)
       
       Similar warning found at 3 other locations:
         nofile:4:12: Sample.a/0
         nofile:5:12: Sample.a/0
         nofile:6:12: Sample.a/0

      """

      assert capture_eval(source) =~ expected
    after
      purge(Sample)
    end

    @tag :tmp_dir
    test "file + line + column", %{tmp_dir: tmp_dir} do
      path = make_relative_tmp(tmp_dir, "warning_group_nofile.ex")

      source = """
      defmodule Sample do
        @file "#{path}"

        def a do
          Unknown.bar()
          Unknown.bar()
          Unknown.bar()
          Unknown.bar()
        end
      end
      """

      File.write!(path, source)

      expected = """
          â”Œâ”€ warning: #{path}:5:12: Sample.a/0
          â”‚
        5 â”‚     Unknown.bar()
          â”‚            ~
          â”‚
          Unknown.bar/0 is undefined (module Unknown is not available or is yet to be defined)
          
          Similar warning found at 3 other locations:
            #{path}:6:12: Sample.a/0
            #{path}:7:12: Sample.a/0
            #{path}:8:12: Sample.a/0

      """

      assert capture_eval(source) == expected
    after
      purge(Sample)
    end

    @tag :tmp_dir
    test "file + line", %{tmp_dir: tmp_dir} do
      path = make_relative_tmp(tmp_dir, "warning_group_nofile.ex")

      source = """
      defmodule Sample do
        @file "#{path}"

        def a do
          Unknown.bar()
          Unknown.bar()
          Unknown.bar()
          Unknown.bar()
        end
      end
      """

      File.write!(path, source)

      expected = """
          â”Œâ”€ warning: #{path}:5: Sample.a/0
          â”‚
        5 â”‚     Unknown.bar()
          â”‚     ~~~~~~~~~~~~~
          â”‚
          Unknown.bar/0 is undefined (module Unknown is not available or is yet to be defined)
          
          Similar warning found at 3 other locations:
            #{path}:6: Sample.a/0
            #{path}:7: Sample.a/0
            #{path}:8: Sample.a/0

      """

      assert capture_eval(source, false) == expected
    after
      purge(Sample)
    end
  end

  defp make_relative_tmp(tmp_dir, filename) do
    # Compiler outputs relative, so we just grab the tmp dir
    tmp_dir
    |> Path.join(filename)
    |> Path.relative_to_cwd()
  end

  defp capture_eval(source, columns? \\ true) do
    capture_io(:stderr, fn ->
      quoted = Code.string_to_quoted!(source, columns: columns?)
      Code.eval_quoted(quoted)
    end)
  end

  defp capture_raise(source, exception, mock_stacktrace \\ []) do
    e =
      assert_raise exception, fn ->
        ast = Code.string_to_quoted!(source, columns: true)
        Code.eval_quoted(ast)
      end

    Exception.format(:error, e, mock_stacktrace)
  end

  defp purge(module) when is_atom(module) do
    :code.purge(module)
    :code.delete(module)
  end
end
